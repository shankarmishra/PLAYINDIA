buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 36
        ndkVersion = "27.0.12077973"
        kotlinVersion = "2.1.20"
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

apply plugin: "com.facebook.react.rootproject"

subprojects {
    // Force c++_shared for all Android Library subprojects (e.g., native modules)
    pluginManager.withPlugin("com.android.library") {
        android {
            defaultConfig {
                externalNativeBuild {
                    cmake {
                        arguments "-DANDROID_STL=c++_shared", "-DCMAKE_SHARED_LINKER_FLAGS=-lc++_shared"
                    }
                }
            }
        }
    }

    // Force c++_shared for Android Application subprojects (e.g., app module)
    pluginManager.withPlugin("com.android.application") {
        android {
            defaultConfig {
                externalNativeBuild {
                    cmake {
                        arguments "-DANDROID_STL=c++_shared", "-DCMAKE_SHARED_LINKER_FLAGS=-lc++_shared"
                    }
                }
            }
        }
    }

    // Fix for async-storage codegen issue on Windows/OneDrive
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.tasks.configureEach { task ->
                if (task.name.contains("generateCodegenSchemaFromJavaScript")) {
                    task.doNotTrackState("Workaround for OneDrive symlink issue")
                }
            }
        }
    }
}
